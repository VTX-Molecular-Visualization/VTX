#version 450 core
#include "include/helper.glsl"
#include "../constant.glsl"

#ifndef LOCAL_SIZE_X
	#define LOCAL_SIZE_X 1
#endif
#ifndef LOCAL_SIZE_Y
	#define LOCAL_SIZE_Y 1
#endif
#ifndef LOCAL_SIZE_Z
	#define LOCAL_SIZE_Z 1
#endif

layout (local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_Y, local_size_z = LOCAL_SIZE_Z) in;

// Uniforms.
uniform uint uSize;

// SSBO.
layout (std140, binding = 0) readonly buffer Positions
{
	vec4[] positions;
};

layout (std140, binding = 1) buffer Normals
{
	vec4[] normals;
};

layout (std430, binding = 2) readonly buffer Indices
{
	uint[] indices;
};

layout (std140, binding = 3) buffer NormalsCasted
{
	ivec4[] normalsCasted;
};

// May be useful.
#define atomicAddFloat(MEM, INDATA)\
{ \
    uint expected_mem = MEM; \
    float input_mem = (uintBitsToFloat(MEM) + INDATA); \
    uint returned_mem = atomicCompSwap(MEM, expected_mem, floatBitsToUint(input_mem)); \
    while(returned_mem != expected_mem){ \
        expected_mem = returned_mem; \
        input_mem = (uintBitsToFloat(expected_mem) + INDATA); \
        returned_mem = atomicCompSwap(MEM, expected_mem, floatBitsToUint(input_mem)); \
    } \
}

void main()
{
	uint hash = globalInvocationHash( gl_GlobalInvocationID, gl_NumWorkGroups, gl_WorkGroupSize );
	
	hash *= 3;
	
	if( hash >= uSize - 2 )
	{
		return;
	}

	const vec3 normal = normalize (
							cross( 
								vec3( positions[ indices[ hash + 1 ] ] - positions[ indices[ hash + 2 ] ] ),
								vec3( positions[ indices[ hash + 1 ] ] - positions[ indices[ hash + 0 ] ] ) ) );

	for( uint i = 0; i < 3; ++i )
	{
		atomicAdd( normalsCasted[ indices[ hash + i ] ].x, int( normal.x / EPSILON ) );
		atomicAdd( normalsCasted[ indices[ hash + i ] ].y, int( normal.y / EPSILON ) );
		atomicAdd( normalsCasted[ indices[ hash + i ] ].z, int( normal.z / EPSILON ) );

		//atomicAddFloat( normalsCasted[ indices[ hash + i ] ].x, floatBitsToInt( normal.x ) );
		//atomicAddFloat( normalsCasted[ indices[ hash + i ] ].y, floatBitsToInt( normal.y ) );
		//atomicAddFloat( normalsCasted[ indices[ hash + i ] ].z, floatBitsToInt( normal.z ) );
	
	}
}