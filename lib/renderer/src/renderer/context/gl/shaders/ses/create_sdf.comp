#version 450 core
#include "../constant.glsl"
#include "include/struct.glsl"
#include "include/helper.glsl"
#include "include/grid.glsl"

#ifndef LOCAL_SIZE_X
	#define LOCAL_SIZE_X 1
#endif
#ifndef LOCAL_SIZE_Y
	#define LOCAL_SIZE_Y 1
#endif
#ifndef LOCAL_SIZE_Z
	#define LOCAL_SIZE_Z 1
#endif
layout (local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_Y, local_size_z = LOCAL_SIZE_Z) in;

// Uniforms.
uniform vec3	uGridAtomWorldOrigin;
uniform vec3	uGridSESWorldOrigin;
uniform uvec3	uGridAtomSize;
uniform uvec3	uGridSESSize;
uniform vec3	uGridAtomCellSize;
uniform vec3	uGridSESCellSize;
uniform uint	uGridAtomCellCount;
uniform uint	uGridSESCellCount;
uniform float	uProbeRadius;
uniform float	uVoxelSize;

// Data struct.
struct Range
{
	uint first;
	uint count;
};

// SSBO.
layout (std430, binding = 0) writeonly buffer SESGridData
{
	SESGridDataStruct[] sesGridData;
};

layout (std430, binding = 1) readonly buffer AtomGridData
{ 
	Range[] atomGridData;
};

layout (std430, binding = 2) readonly buffer AtomIndex
{ 
	uint[] atomIndex;
};

layout (std140, binding = 3) readonly buffer AtomPositionsVdW
{ 
	vec4[] atomPositionsVdW;
};

void main()
{
	// Get corresponding ses grid data.	
	const uint sesGridHash = globalInvocationHash( gl_GlobalInvocationID, gl_NumWorkGroups, gl_WorkGroupSize );
	
	// Not in grid.
	if ( sesGridHash >= uGridSESCellCount )
	{				
		return;
	}

	const uvec3 sesGridPosition = gridPosition( sesGridHash, uGridSESSize);

	// Default value.
	sesGridData[ sesGridHash ].sdf = uProbeRadius;
	sesGridData[ sesGridHash ].nearestAtom = -1;
				
	// Get corresponding acceleration grid data.
	const vec3 sesGridCellWorldPosition = worldPosition( sesGridPosition, uGridSESWorldOrigin, uGridSESCellSize);
	const uvec3 atomGridPosition = gridPosition( sesGridCellWorldPosition, uGridAtomWorldOrigin, uGridAtomCellSize);

	// Loop over cells to visit.
	float minDistance = FLT_MAX;
			
	for ( int ox = -1; ox <= 1; ++ox )
	{
		for ( int oy = -1; oy <= 1; ++oy )
		{		
			for ( int oz = -1; oz <= 1; ++oz )
			{					
				const vec3 offset = vec3( ox, oy, oz );				
				const uvec3 gridPositionToVisit = uvec3( atomGridPosition + offset );					
				const uint hashToVisit = gridHash( gridPositionToVisit, uGridAtomSize );					

				if ( hashToVisit >= uGridAtomCellCount )
				{				
					continue;
				}		

				const uint first = atomGridData[ hashToVisit ].first;
				const uint count = atomGridData[ hashToVisit ].count;				

				// Compute SDF.
				for ( uint i = first; i < first + count; ++i )
				{		
					const uint index = atomIndex[ i ];					
					float d = distance( atomPositionsVdW[ index ].xyz, sesGridCellWorldPosition );
					
					// Inside.
					if ( d < uVoxelSize )
					{
						sesGridData[ sesGridHash ].sdf = -uVoxelSize;	
						sesGridData[ sesGridHash ].nearestAtom = -1;						
						// Don't need to loop over other cells.
						return;
					}
					// Boundary.
					else
					{					
						d -= ( uProbeRadius + atomPositionsVdW[ index ].w );
						if ( d < 0.f )
						{
							sesGridData[ sesGridHash ].sdf = -uVoxelSize;
							if ( d < minDistance )
							{
								minDistance	= d;
								sesGridData[ sesGridHash ].nearestAtom = int( index );								
							}
						}						
					}
				}
			}
		}
	}
}